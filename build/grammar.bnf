<prog> ::= as principal p password s do \n <cmd> ***
<cmd> ::= exit \n | return <expression> \n | <prim_cmd> \n <cmd>
<expression> ::=  <value> | [] | { <fieldvals> }
<fieldvals> ::=  x = <value> | x = <value> , <fieldvals>
<value> ::=  x | x . y | s
<prim_cmd> ::=
          create principal p s
        | change password p s
        | set x = <expression>
        | append to x with <expression>
        | local x = <expression>
        | foreach y in x replacewith <expression>
        | set delegation <tgt> q <right> -> p
        | delete delegation <tgt> q <right> -> p
        | default delegator = p
<tgt> ::= all | x
<right> ::= read | write | append | delegate